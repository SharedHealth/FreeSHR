apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'rpm'
apply plugin: 'idea'
apply plugin: 'cobertura'

mainClassName = "org.freeshr.launch.Main"

jar {
    baseName = 'freeshr'
    version = '0.1-SNAPSHOT'
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
}

test {
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed"
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // set heap size for the test JVM(s)
    minHeapSize = "512m"
    maxHeapSize = "1024m"

    // set JVM arguments for the test JVM(s)
    jvmArgs '-XX:MaxPermSize=256m'

//    onOutput { descriptor, event ->
//        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
//    }
}

test.finalizedBy(stopMockClientIndex)

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web:1.1.1.RELEASE') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: "logback-classic"
    }
    compile libraries.snappy, libraries.spring_data_cassandra, libraries.spring_boot_logging,
            libraries.apache_commons_io, libraries.spring_framework, libraries.fhir_validator,
            libraries.jackson, libraries.saxon, libraries.rxjava,
            libraries.netflix_astyanax, libraries.rome, libraries.time_uuid, libraries.spring_security,
            libraries.spring_web, libraries.eclipse_moxy

    compile(libraries.spring_boot_security){
        exclude group: 'ch.qos.logback', module: "logback-classic"
    }
    compile('io.netty:netty:3.9.0.Final') {
        force = 'true'
    }
    compile('com.toddfast.mutagen:mutagen-cassandra:0.4.0') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compile project(":fhir")
    testCompile testLibraries.mockito, testLibraries.junit, testLibraries.spring_test, testLibraries.cassandra_unit_spring,
            testLibraries.wiremock, testLibraries.hamcrest_core
}

configurations.all {
    exclude group: 'org.jboss.netty', module: 'netty'
}

//build artifact
private Properties loadConfig() {
    Properties properties = new Properties()
    properties.load(new FileInputStream(file("${projectDir}/../env/local.properties")));
    properties
}

task assembly(dependsOn: 'build') << {
    new File("${buildDir}/etc/").mkdir();
}

task copyZip(type: Copy, dependsOn: 'assembly') {
    from 'src/main/resources/validation.zip'
    into 'build/etc/'
}

task exportProperties(dependsOn: 'copyZip') << {
    PrintStream output = new PrintStream(new FileOutputStream(file("${buildDir}/etc/bdshr")));
    Properties properties = loadConfig()
    properties.each { prop, val ->
        output.println("export " + prop + "=" + val)
    }
}

task dist(dependsOn: 'exportProperties', type: Rpm) {
    packageName = 'shr'
    version = '0.1'
    release = 1
    arch = NOARCH
    os = LINUX

    into '/opt/bdshr'

    postInstall = file('scripts/utils/postInstall.sh')
    preUninstall = file('scripts/utils/preUninstall.sh')
    postUninstall = file('scripts/utils/postUninstall.sh')

    from("${buildDir}/etc") {
        fileMode = 0755
        into 'etc'
    }

    from("scripts/rpm") {
        fileMode = 0755
        exclude 'placeholder'
        into 'bin'
    }

    from("${buildDir}/libs") {
        fileMode = 0755
        into 'lib'
    }

    from("scripts/rpm") {
        fileMode = 0755
        exclude 'bdshr'
        into 'var'
    }
}

war {
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath(
                "org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE",
                'com.netflix.nebula:gradle-ospackage-plugin:2.+',
                'net.saliman:gradle-cobertura-plugin:2.2.4'
        )
    }
}

cobertura {
    coverageFormats = ['html', 'xml']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura")
}

test.finalizedBy(project.tasks.cobertura)
