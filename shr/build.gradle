apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'rpm'
apply plugin: 'idea'

mainClassName = "org.freeshr.launch.Main"

jar {
    baseName = 'freeshr'
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    flatDir {
        dirs '../lib'
    }
}


configurations.all {
    exclude group: 'org.jboss.netty', module: 'netty'
}

test {
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed"
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // set heap size for the test JVM(s)
    minHeapSize = "512m"
    maxHeapSize = "1024m"

    // set JVM arguments for the test JVM(s)
    jvmArgs '-XX:MaxPermSize=256m'

//    onOutput { descriptor, event ->
//        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
//    }
}

test.finalizedBy(stopMockClientIndex)

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web:1.1.1.RELEASE') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: "logback-classic"
    }
    compile libraries.snappy, libraries.spring_data_cassandra, libraries.spring_boot_logging,
            libraries.apache_commons_io, libraries.spring_framework,
            libraries.jackson, libraries.saxon, libraries.rxjava,
            libraries.rome, libraries.rome_tools, libraries.time_uuid,
            libraries.spring_security, libraries.spring_web, libraries.eclipse_moxy,
            libraries.spring_context_support, libraries.ehcache_core, libraries.commons_lang,
            libraries.lambdaj, libraries.joda_time, libraries.lz4,libraries.httpAsyncClient,
            libraries.hapi_fhir_base, libraries.hapi_fhir_structures, libraries.hapi_fhir_validation_resources, libraries.hapi_fhir_structures_hl7,
            libraries.phloc_schematron, libraries.phloc_commons, libraries.hector_core

    compile(libraries.spring_boot_security) {
        exclude group: 'ch.qos.logback', module: "logback-classic"
    }
    compile(libraries.spring_boot_actuator) {
        exclude group: 'ch.qos.logback', module: "logback-classic"
    }

    testCompile(testLibraries.netty) {
        force = 'true'
    }
    testCompile project(":schema")
    testCompile testLibraries.mockito, testLibraries.junit, testLibraries.spring_test, testLibraries.cassandra_unit_spring,
            testLibraries.wiremock, testLibraries.hamcrest_core
}

//build artifact
private Properties loadConfig() {
    Properties properties = new Properties()
    properties.load(new FileInputStream(file("${projectDir}/../env/local.properties")));
    properties
}

task assembly(dependsOn: 'build') << {
    new File("${buildDir}/etc/").mkdir();
}

task copyZip(type: Copy, dependsOn: 'assembly') {
    from 'src/main/resources/validation.zip'
    into 'build/etc/'
}

task exportProperties(dependsOn: 'copyZip') << {
    PrintStream output = new PrintStream(new FileOutputStream(file("${buildDir}/etc/bdshr")));
    Properties properties = loadConfig()
    properties.each { prop, val ->
        output.println("export " + prop + "=" + val)
    }
}

task dist(dependsOn: [project(":schema").tasks.build, 'exportProperties'], type: Rpm) {
    packageName = 'shr'
    version = '2.6'
    release = project.hasProperty('release') ? project.release : '1'
    arch = NOARCH
    os = LINUX

    into '/opt/bdshr'

    postInstall = file('scripts/utils/postInstall.sh')
    preUninstall = file('scripts/utils/preUninstall.sh')
    postUninstall = file('scripts/utils/postUninstall.sh')

    from("${buildDir}/etc") {
        fileMode = 0755
        into 'etc'
    }

    from("scripts/rpm") {
        fileMode = 0755
        exclude 'placeholder'
        into 'bin'
    }

    from("${buildDir}/libs") {
        fileMode = 0755
        into 'lib'
    }

    from("${rootDir}/schema/scripts/rpm") {
        fileMode = 0755
        into 'bin'
    }

    from("${rootDir}/schema/build/libs") {
        fileMode = 0755
        into 'lib'
    }

    from("scripts/rpm") {
        fileMode = 0755
        exclude 'bdshr'
        exclude 'start.sh'
        into 'var'
    }
}

war {
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

task runSHR(type: JavaExec) {
    Properties properties = new Properties();
    properties.load(new FileInputStream(file('../env/local.properties')))
    main = mainClassName
    jvmArgs = ['-DSHR_LOG_LEVEL=DEBUG']
    classpath = sourceSets.main.runtimeClasspath
    environment properties
}

task debugSHR(type: JavaExec) {
    Properties properties = new Properties();
    properties.load(new FileInputStream(file('../env/local.properties')))
    main = mainClassName
    jvmArgs = ['-DSHR_LOG_LEVEL=DEBUG']
    classpath = sourceSets.main.runtimeClasspath
    environment properties
    debug = true
}


buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath(
                "org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE",
                'com.netflix.nebula:gradle-ospackage-plugin:2.+',
                'net.saliman:gradle-cobertura-plugin:2.2.4'
        )
    }
}
